buildscript {
    ext {
        springBootVersion = "2.1.3.RELEASE"
        querydslVersion = "4.2.1"
        querydslPluginVersion = "1.0.10"
    }

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
        classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:${querydslPluginVersion}"
    }
}

subprojects {
    group "com.github.uuidcode"
    version "1.0"

    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.ewerk.gradle.plugins.querydsl"

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compile ("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "com.querydsl:querydsl-core:${querydslVersion}"
        compile "com.querydsl:querydsl-jpa:${querydslVersion}"
        compile "com.querydsl:querydsl-apt:${querydslVersion}"
        compile "commons-dbcp:commons-dbcp:1.4"
        compile "mysql:mysql-connector-java:5.1.47"
        compile "ch.qos.logback:logback-core:1.2.3"
        compile "org.slf4j:slf4j-api:1.7.5"
        compile "p6spy:p6spy:3.6.0"
        compile "org.mybatis.spring.boot:mybatis-spring-boot-starter:2.0.0"
        compile "pl.allegro.tech.boot:handlebars-spring-boot-starter:0.3.0"

        compile "org.projectlombok:lombok:1.18.6"

        annotationProcessor (
                "org.projectlombok:lombok:1.18.6",
                "com.querydsl:querydsl-apt:${querydslVersion}"
        )

        testCompile 'org.junit.jupiter:junit-jupiter:5.4.0'
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.0'
        testCompile ("org.junit.jupiter:junit-jupiter-engine:5.4.0") {
            exclude group: 'org.junit.platform', module: 'junit-platform-commons'
        }
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.4.0'
        testCompile 'org.junit.platform:junit-platform-launcher:1.4.0'
        testCompile 'org.junit.platform:junit-platform-commons:1.4.0'
        testCompile 'org.assertj:assertj-core:3.11.1'
        testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    }

    def querydslSrcDir = 'src/main/generated'

    querydsl {
        library = "com.querydsl:querydsl-apt"
        jpa = true
        querydslSourcesDir = querydslSrcDir
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', querydslSrcDir]
            }
        }
    }
}

project(':core') {
    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }
}

project(':service') {
    dependencies {
        compile project(':core')
    }

    bootJar {
        mainClassName = 'com.github.uuidcode.service.Application'
    }
}

project(':api') {
    dependencies {
        compile project(':core')
    }

    bootJar {
        mainClassName = 'com.github.uuidcode.api.Application'
    }
}